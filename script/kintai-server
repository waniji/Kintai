#!perl
use strict;
use warnings;
use utf8;
use File::Spec;
use File::Basename;
use lib File::Spec->catdir(dirname(__FILE__), '../lib');
use Plack::Builder;

use Kintai::Web;
use Kintai::API;
use Kintai;
use URI::Escape;
use File::Path ();

my $app = builder {

    enable 'Plack::Middleware::ReverseProxy';

    my $api = Kintai::API->to_app();
    my $web = builder {
        enable 'Plack::Middleware::Static',
            path => qr{^(?:/static/)},
            root => File::Spec->catdir(dirname(__FILE__), '..');
        enable 'Plack::Middleware::Static',
            path => qr{^(?:/robots\.txt|/favicon\.ico)$},
            root => File::Spec->catdir(dirname(__FILE__), '..', 'static');
        Kintai::Web->to_app();
    };

    sub {
        my $env = shift;
        if( $env->{PATH_INFO} =~ m{^/api/} ) {
            $api->($env);
        } else {
            $web->($env);
        }
    }

};
unless (caller) {
    my $port        = 5000;
    my $host        = '127.0.0.1';
    my $max_workers = 4;

    require Getopt::Long;
    require Plack::Loader::Shotgun;
    my $p = Getopt::Long::Parser->new(
        config => [qw(posix_default no_ignore_case auto_help)]
    );
    $p->getoptions(
        'p|port=i'      => \$port,
        'host=s'        => \$host,
        'max-workers=i' => \$max_workers,
        'version!'      => \my $version,
        'c|config=s'    => \my $config_file,
    );
    if ($version) {
        print "Kintai: $Kintai::VERSION\n";
        exit 0;
    }
    if ($config_file) {
        my $config = do $config_file;
        Carp::croak("$config_file: $@") if $@;
        Carp::croak("$config_file: $!") unless defined $config;
        unless ( ref($config) eq 'HASH' ) {
            Carp::croak("$config_file does not return HashRef.");
        }
        no warnings 'redefine';
        no warnings 'once';
        *Kintai::load_config = sub { $config }
    }

    print "Kintai: http://${host}:${port}/\n";

    my $loader = Plack::Loader::Shotgun->load('Starlet',
        port        => $port,
        host        => $host,
        max_workers => $max_workers,
    );
    return $loader->run($app);
}
return $app;
